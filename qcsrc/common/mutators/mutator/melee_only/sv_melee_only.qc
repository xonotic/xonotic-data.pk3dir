#include "sv_melee_only.qh"

#include "../overkill/sv_overkill.qh"

string autocvar_g_melee_only;
REGISTER_MUTATOR(melee_only, expr_evaluate(autocvar_g_melee_only) && !MUTATOR_IS_ENABLED(mutator_instagib) && !MUTATOR_IS_ENABLED(ok) && !g_nexball);

MUTATOR_HOOKFUNCTION(melee_only, SetStartItems, CBC_ORDER_LAST)
{
	start_ammo_shells = warmup_start_ammo_shells = 0;
}

MUTATOR_HOOKFUNCTION(melee_only, SetWeaponArena)
{
	// turn weapon arena off
	M_ARGV(0, string) = "off";
}

// CBC_ORDER_LAST - must override LMS and similar
MUTATOR_HOOKFUNCTION(melee_only, PlayerSpawn, CBC_ORDER_LAST)
{
	entity player = M_ARGV(0, entity);

	STAT(WEAPONS, player) = WEPSET(SHOTGUN);
}

MUTATOR_HOOKFUNCTION(melee_only, ForbidRandomStartWeapons)
{
	return true;
}

MUTATOR_HOOKFUNCTION(melee_only, ForbidThrowCurrentWeapon)
{
	return true;
}

MUTATOR_HOOKFUNCTION(melee_only, FilterItemDefinition)
{
	entity definition = M_ARGV(0, entity);

	switch (definition)
	{
		case ITEM_HealthSmall:
		case ITEM_ArmorSmall:
			return false;
	}

	return true;
}

MUTATOR_HOOKFUNCTION(melee_only, BuildMutatorsString)
{
	M_ARGV(0, string) = strcat(M_ARGV(0, string), ":MeleeOnly");
}

MUTATOR_HOOKFUNCTION(melee_only, BuildMutatorsPrettyString)
{
	M_ARGV(0, string) = strcat(M_ARGV(0, string), ", Melee Only Arena");
}
