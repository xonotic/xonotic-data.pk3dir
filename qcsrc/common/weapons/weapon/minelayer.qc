#ifndef IMPLEMENTATION
CLASS(MineLayer, Weapon)
/* ammotype  */ ATTRIB(MineLayer, ammo_field, .int, ammo_rockets)
/* impulse   */ ATTRIB(MineLayer, impulse, int, 4)
/* flags     */ ATTRIB(MineLayer, spawnflags, int, WEP_FLAG_MUTATORBLOCKED | WEP_FLAG_RELOADABLE | WEP_TYPE_SPLASH);
/* rating    */ ATTRIB(MineLayer, bot_pickupbasevalue, float, BOT_PICKUP_RATING_HIGH);
/* color     */ ATTRIB(MineLayer, wpcolor, vector, '0.75 1 0');
/* modelname */ ATTRIB(MineLayer, mdl, string, "minelayer");
#ifndef MENUQC
/* model     */ ATTRIB(MineLayer, m_model, Model, MDL_MINELAYER_ITEM);
#endif
/* crosshair */ ATTRIB(MineLayer, w_crosshair, string, "gfx/crosshairminelayer");
/* crosshair */ ATTRIB(MineLayer, w_crosshair_size, float, 0.9);
/* wepimg    */ ATTRIB(MineLayer, model2, string, "weaponminelayer");
/* refname   */ ATTRIB(MineLayer, netname, string, "minelayer");
/* wepname   */ ATTRIB(MineLayer, m_name, string, _("Mine Layer"));

#define X(BEGIN, P, END, class, prefix) \
	BEGIN(class) \
		P(class, prefix, ammo, float, NONE) \
		P(class, prefix, animtime, float, NONE) \
		P(class, prefix, damageforcescale, float, NONE) \
		P(class, prefix, damage, float, NONE) \
		P(class, prefix, detonatedelay, float, NONE) \
		P(class, prefix, edgedamage, float, NONE) \
		P(class, prefix, force, float, NONE) \
		P(class, prefix, health, float, NONE) \
		P(class, prefix, lifetime, float, NONE) \
		P(class, prefix, lifetime_countdown, float, NONE) \
		P(class, prefix, limit, float, NONE) \
		P(class, prefix, protection, float, NONE) \
		P(class, prefix, proximityradius, float, NONE) \
		P(class, prefix, radius, float, NONE) \
		P(class, prefix, refire, float, NONE) \
		P(class, prefix, reload_ammo, float, NONE) \
        P(class, prefix, reload_time, float, NONE) \
		P(class, prefix, remote_damage, float, NONE) \
		P(class, prefix, remote_edgedamage, float, NONE) \
		P(class, prefix, remote_force, float, NONE) \
		P(class, prefix, remote_radius, float, NONE) \
		P(class, prefix, speed, float, NONE) \
        P(class, prefix, switchdelay_drop, float, NONE) \
        P(class, prefix, switchdelay_raise, float, NONE) \
		P(class, prefix, time, float, NONE) \
        P(class, prefix, weaponreplace, string, NONE) \
        P(class, prefix, weaponstartoverride, float, NONE) \
        P(class, prefix, weaponstart, float, NONE) \
        P(class, prefix, weaponthrowable, float, NONE) \
	END()
    W_PROPS(X, MineLayer, minelayer)
#undef X
ENDCLASS(MineLayer)
REGISTER_WEAPON(MINE_LAYER, minelayer, NEW(MineLayer));

#ifdef SVQC
void W_MineLayer_Think();
.float minelayer_detonate, mine_explodeanyway;
.float mine_time;
.vector mine_orientation;
#endif
#endif
#ifdef IMPLEMENTATION
#ifdef SVQC
spawnfunc(weapon_minelayer) { weapon_defaultspawnfunc(this, WEP_MINE_LAYER); }

void W_MineLayer_Stick(entity to)
{SELFPARAM();
	spamsound(self, CH_SHOTS, SND(MINE_STICK), VOL_BASE, ATTN_NORM);

	// in order for mines to face properly when sticking to the ground, they must be a server side entity rather than a csqc projectile

	entity newmine = spawn();
	newmine.classname = self.classname;

	newmine.bot_dodge = self.bot_dodge;
	newmine.bot_dodgerating = self.bot_dodgerating;

	newmine.owner = self.owner;
	newmine.realowner = self.realowner;
	setsize(newmine, '-4 -4 -4', '4 4 4');
	setorigin(newmine, self.origin);
	setmodel(newmine, MDL_MINELAYER_MINE);
	newmine.angles = vectoangles(-trace_plane_normal); // face against the surface

	newmine.mine_orientation = -trace_plane_normal;

	newmine.takedamage = self.takedamage;
	newmine.damageforcescale = self.damageforcescale;
	newmine.health = self.health;
	newmine.event_damage = self.event_damage;
	newmine.spawnshieldtime = self.spawnshieldtime;
	newmine.damagedbycontents = true;

	newmine.movetype = MOVETYPE_NONE; // lock the mine in place
	newmine.projectiledeathtype = self.projectiledeathtype;

	newmine.mine_time = self.mine_time;

	newmine.touch = func_null;
	newmine.think = W_MineLayer_Think;
	newmine.nextthink = time;
	newmine.cnt = self.cnt;
	newmine.flags = self.flags;

	remove(self);
	setself(newmine);

	if(to)
		SetMovetypeFollow(self, to);
}

void W_MineLayer_Explode()
{SELFPARAM();
	if(other.takedamage == DAMAGE_AIM)
		if(IS_PLAYER(other))
			if(DIFF_TEAM(self.realowner, other))
				if(!IS_DEAD(other))
					if(IsFlying(other))
						Send_Notification(NOTIF_ONE, self.realowner, MSG_ANNCE, ANNCE_ACHIEVEMENT_AIRSHOT);

	self.event_damage = func_null;
	self.takedamage = DAMAGE_NO;

	RadiusDamage(self, self.realowner, WEP_CVAR(minelayer, damage), WEP_CVAR(minelayer, edgedamage), WEP_CVAR(minelayer, radius), world, world, WEP_CVAR(minelayer, force), self.projectiledeathtype, other);

	if(PS(self.realowner).m_weapon == WEP_MINE_LAYER)
	{
		setself(self.realowner);
		Weapon w = WEP_MINE_LAYER;
		if(!w.wr_checkammo1(w))
		{
			self.cnt = WEP_MINE_LAYER.m_id;
			int slot = 0; // TODO: unhardcode
			ATTACK_FINISHED(self, slot) = time;
			PS(self).m_switchweapon = w_getbestweapon(self);
		}
		setself(this);
	}
	self.realowner.minelayer_mines -= 1;
	remove(self);
}

void W_MineLayer_DoRemoteExplode()
{SELFPARAM();
	self.event_damage = func_null;
	self.takedamage = DAMAGE_NO;

	if(self.movetype == MOVETYPE_NONE || self.movetype == MOVETYPE_FOLLOW)
		self.velocity = self.mine_orientation; // particle fx and decals need .velocity

	RadiusDamage(self, self.realowner, WEP_CVAR(minelayer, remote_damage), WEP_CVAR(minelayer, remote_edgedamage), WEP_CVAR(minelayer, remote_radius), world, world, WEP_CVAR(minelayer, remote_force), self.projectiledeathtype | HITTYPE_BOUNCE, world);

	if(PS(self.realowner).m_weapon == WEP_MINE_LAYER)
	{
		setself(self.realowner);
		Weapon w = WEP_MINE_LAYER;
		if(!w.wr_checkammo1(w))
		{
			self.cnt = WEP_MINE_LAYER.m_id;
			int slot = 0; // TODO: unhardcode
			ATTACK_FINISHED(self, slot) = time;
			PS(self).m_switchweapon = w_getbestweapon(self);
		}
		setself(this);
	}
	self.realowner.minelayer_mines -= 1;
	remove(self);
}

void W_MineLayer_RemoteExplode()
{SELFPARAM();
	if(!IS_DEAD(self.realowner))
		if((self.spawnshieldtime >= 0)
			? (time >= self.spawnshieldtime) // timer
			: (vlen(NearestPointOnBox(self.realowner, self.origin) - self.origin) > WEP_CVAR(minelayer, remote_radius)) // safety device
		)
		{
			W_MineLayer_DoRemoteExplode();
		}
}

void W_MineLayer_ProximityExplode()
{SELFPARAM();
	// make sure no friend is in the mine's radius. If there is any, explosion is delayed until he's at a safe distance
	if(WEP_CVAR(minelayer, protection) && self.mine_explodeanyway == 0)
	{
		entity head;
		head = findradius(self.origin, WEP_CVAR(minelayer, radius));
		while(head)
		{
			if(head == self.realowner || SAME_TEAM(head, self.realowner))
				return;
			head = head.chain;
		}
	}

	self.mine_time = 0;
	W_MineLayer_Explode();
}

int W_MineLayer_Count(entity e)
{
	int minecount = 0;
	entity mine;
	for(mine = world; (mine = find(mine, classname, "mine")); ) if(mine.realowner == e)
		minecount += 1;

	return minecount;
}

void W_MineLayer_Think()
{SELFPARAM();
	entity head;

	self.nextthink = time;

	if(self.movetype == MOVETYPE_FOLLOW)
	{
		if(LostMovetypeFollow(self))
		{
			UnsetMovetypeFollow(self);
			self.movetype = MOVETYPE_NONE;
		}
	}

	// our lifetime has expired, it's time to die - mine_time just allows us to play a sound for this
	// TODO: replace this mine_trigger.wav sound with a real countdown
	if((time > self.cnt) && (!self.mine_time) && (self.cnt > 0))
	{
		if(WEP_CVAR(minelayer, lifetime_countdown) > 0)
			spamsound(self, CH_SHOTS, SND(MINE_TRIGGER), VOL_BASE, ATTN_NORM);
		self.mine_time = time + WEP_CVAR(minelayer, lifetime_countdown);
		self.mine_explodeanyway = 1; // make the mine super aggressive -- Samual: Rather, make it not care if a team mate is near.
	}

	// a player's mines shall explode if he disconnects or dies
	// TODO: Do this on team change too -- Samual: But isn't a player killed when they switch teams?
	if(!IS_PLAYER(self.realowner) || IS_DEAD(self.realowner) || STAT(FROZEN, self.realowner))
	{
		other = world;
		self.projectiledeathtype |= HITTYPE_BOUNCE;
		W_MineLayer_Explode();
		return;
	}

	// set the mine for detonation when a foe gets close enough
	head = findradius(self.origin, WEP_CVAR(minelayer, proximityradius));
	while(head)
	{
		if(IS_PLAYER(head) && !IS_DEAD(head) && !STAT(FROZEN, head))
		if(head != self.realowner && DIFF_TEAM(head, self.realowner)) // don't trigger for team mates
		if(!self.mine_time)
		{
			spamsound(self, CH_SHOTS, SND(MINE_TRIGGER), VOL_BASE, ATTN_NORM);
			self.mine_time = time + WEP_CVAR(minelayer, time);
		}
		head = head.chain;
	}

	// explode if it's time to
	if(self.mine_time && time >= self.mine_time)
	{
		W_MineLayer_ProximityExplode();
		return;
	}

	// remote detonation
	if(PS(self.realowner).m_weapon == WEP_MINE_LAYER)
	if(!IS_DEAD(self.realowner))
	if(self.minelayer_detonate)
		W_MineLayer_RemoteExplode();
}

void W_MineLayer_Touch()
{SELFPARAM();
	if(self.movetype == MOVETYPE_NONE || self.movetype == MOVETYPE_FOLLOW)
		return; // we're already a stuck mine, why do we get called? TODO does this even happen?

	if(WarpZone_Projectile_Touch())
	{
		if(wasfreed(self))
			self.realowner.minelayer_mines -= 1;
		return;
	}

	if(other && IS_PLAYER(other) && !IS_DEAD(other))
	{
		// hit a player
		// don't stick
	}
	else
	{
		W_MineLayer_Stick(other);
	}
}

void W_MineLayer_Damage(entity inflictor, entity attacker, float damage, int deathtype, vector hitloc, vector force)
{SELFPARAM();
	if(self.health <= 0)
		return;

	float is_from_enemy = (inflictor.realowner != self.realowner);

	if(!W_CheckProjectileDamage(inflictor.realowner, self.realowner, deathtype, (is_from_enemy ? 1 : -1)))
		return; // g_projectiles_damage says to halt

	self.health = self.health - damage;
	self.angles = vectoangles(self.velocity);

	if(self.health <= 0)
		W_PrepareExplosionByDamage(attacker, W_MineLayer_Explode);
}

void W_MineLayer_Attack(Weapon thiswep)
{SELFPARAM();
	entity mine;
	entity flash;

	// scan how many mines we placed, and return if we reached our limit
	if(WEP_CVAR(minelayer, limit))
	{
		if(self.minelayer_mines >= WEP_CVAR(minelayer, limit))
		{
			// the refire delay keeps this message from being spammed
			Send_Notification(NOTIF_ONE, self, MSG_MULTI, WEAPON_MINELAYER_LIMIT, WEP_CVAR(minelayer, limit));
			play2(self, SND(UNAVAILABLE));
			return;
		}
	}

	W_DecreaseAmmo(thiswep, self, WEP_CVAR(minelayer, ammo));

	W_SetupShot_ProjectileSize(self, '-4 -4 -4', '4 4 4', false, 5, SND(MINE_FIRE), CH_WEAPON_A, WEP_CVAR(minelayer, damage));
	Send_Effect(EFFECT_ROCKET_MUZZLEFLASH, w_shotorg, w_shotdir * 1000, 1);

	mine = WarpZone_RefSys_SpawnSameRefSys(self);
	mine.owner = mine.realowner = self;
	if(WEP_CVAR(minelayer, detonatedelay) >= 0)
		mine.spawnshieldtime = time + WEP_CVAR(minelayer, detonatedelay);
	else
		mine.spawnshieldtime = -1;
	mine.classname = "mine";
	mine.bot_dodge = true;
	mine.bot_dodgerating = WEP_CVAR(minelayer, damage) * 2; // * 2 because it can detonate inflight which makes it even more dangerous

	mine.takedamage = DAMAGE_YES;
	mine.damageforcescale = WEP_CVAR(minelayer, damageforcescale);
	mine.health = WEP_CVAR(minelayer, health);
	mine.event_damage = W_MineLayer_Damage;
	mine.damagedbycontents = true;

	mine.movetype = MOVETYPE_TOSS;
	PROJECTILE_MAKETRIGGER(mine);
	mine.projectiledeathtype = WEP_MINE_LAYER.m_id;
	setsize(mine, '-4 -4 -4', '4 4 4'); // give it some size so it can be shot

	setorigin(mine, w_shotorg - v_forward * 4); // move it back so it hits the wall at the right point
	W_SetupProjVelocity_Basic(mine, WEP_CVAR(minelayer, speed), 0);
	mine.angles = vectoangles(mine.velocity);

	mine.touch = W_MineLayer_Touch;
	mine.think = W_MineLayer_Think;
	mine.nextthink = time;
	mine.cnt = (WEP_CVAR(minelayer, lifetime) - WEP_CVAR(minelayer, lifetime_countdown));
	mine.flags = FL_PROJECTILE;
	mine.missile_flags = MIF_SPLASH | MIF_ARC | MIF_PROXY;

	if(mine.cnt > 0) { mine.cnt += time; }

	CSQCProjectile(mine, true, PROJECTILE_MINE, true);

	// muzzle flash for 1st person view
	flash = spawn();
	setmodel(flash, MDL_MINELAYER_MUZZLEFLASH); // precision set below
	SUB_SetFade(flash, time, 0.1);
	flash.effects = EF_ADDITIVE | EF_FULLBRIGHT | EF_LOWPRECISION;
	W_AttachToShotorg(self, flash, '5 0 0');

	// common properties

	MUTATOR_CALLHOOK(EditProjectile, self, mine);

	self.minelayer_mines = W_MineLayer_Count(self);
}

float W_MineLayer_PlacedMines(float detonate)
{SELFPARAM();
	entity mine;
	float minfound = 0;

	for(mine = world; (mine = find(mine, classname, "mine")); ) if(mine.realowner == self)
	{
		if(detonate)
		{
			if(!mine.minelayer_detonate)
			{
				mine.minelayer_detonate = true;
				minfound = 1;
			}
		}
		else
			minfound = 1;
	}
	return minfound;
}

		METHOD(MineLayer, wr_aim, void(entity thiswep))
		{
			// aim and decide to fire if appropriate
			if(self.minelayer_mines >= WEP_CVAR(minelayer, limit))
				self.BUTTON_ATCK = false;
			else
				self.BUTTON_ATCK = bot_aim(WEP_CVAR(minelayer, speed), 0, WEP_CVAR(minelayer, lifetime), false);
			if(skill >= 2) // skill 0 and 1 bots won't detonate mines!
			{
				// decide whether to detonate mines
				entity targetlist, targ;
				float edgedamage, coredamage, edgeradius, recipricoledgeradius, d;
				float selfdamage, teamdamage, enemydamage;
				edgedamage = WEP_CVAR(minelayer, edgedamage);
				coredamage = WEP_CVAR(minelayer, damage);
				edgeradius = WEP_CVAR(minelayer, radius);
				recipricoledgeradius = 1 / edgeradius;
				selfdamage = 0;
				teamdamage = 0;
				enemydamage = 0;
				targetlist = findchainfloat(bot_attack, true);
				entity mine = find(world, classname, "mine");
				while(mine)
				{
					if(mine.realowner != self)
					{
						mine = find(mine, classname, "mine");
						continue;
					}
					targ = targetlist;
					while(targ)
					{
						d = vlen(targ.origin + (targ.mins + targ.maxs) * 0.5 - mine.origin);
						d = bound(0, edgedamage + (coredamage - edgedamage) * sqrt(1 - d * recipricoledgeradius), 10000);
						// count potential damage according to type of target
						if(targ == self)
							selfdamage = selfdamage + d;
						else if(targ.team == self.team && teamplay)
							teamdamage = teamdamage + d;
						else if(bot_shouldattack(targ))
							enemydamage = enemydamage + d;
						targ = targ.chain;
					}
					mine = find(mine, classname, "mine");
				}
				float desirabledamage;
				desirabledamage = enemydamage;
				if(time > self.invincible_finished && time > self.spawnshieldtime)
					desirabledamage = desirabledamage - selfdamage * autocvar_g_balance_selfdamagepercent;
				if(teamplay && self.team)
					desirabledamage = desirabledamage - teamdamage;

				mine = find(world, classname, "mine");
				while(mine)
				{
					if(mine.realowner != self)
					{
						mine = find(mine, classname, "mine");
						continue;
					}
					makevectors(mine.v_angle);
					targ = targetlist;
					if(skill > 9) // normal players only do this for the target they are tracking
					{
						targ = targetlist;
						while(targ)
						{
							if(
								(v_forward * normalize(mine.origin - targ.origin)< 0.1)
								&& desirabledamage > 0.1*coredamage
							)self.BUTTON_ATCK2 = true;
							targ = targ.chain;
						}
					}else{
						float distance; distance= bound(300,vlen(self.origin-self.enemy.origin),30000);
						//As the distance gets larger, a correct detonation gets near imposible
						//Bots are assumed to use the mine spawnfunc_light to see if the mine gets near a player
						if(v_forward * normalize(mine.origin - self.enemy.origin)< 0.1)
							if(IS_PLAYER(self.enemy))
								if(desirabledamage >= 0.1*coredamage)
									if(random()/distance*300 > frametime*bound(0,(10-skill)*0.2,1))
										self.BUTTON_ATCK2 = true;
					//	dprint(ftos(random()/distance*300),">");dprint(ftos(frametime*bound(0,(10-skill)*0.2,1)),"\n");
					}

					mine = find(mine, classname, "mine");
				}
				// if we would be doing at X percent of the core damage, detonate it
				// but don't fire a new shot at the same time!
				if(desirabledamage >= 0.75 * coredamage) //this should do group damage in rare fortunate events
					self.BUTTON_ATCK2 = true;
				if((skill > 6.5) && (selfdamage > self.health))
					self.BUTTON_ATCK2 = false;
				//if(self.BUTTON_ATCK2 == true)
				//	dprint(ftos(desirabledamage),"\n");
				if(self.BUTTON_ATCK2 == true) self.BUTTON_ATCK = false;
			}
		}
		METHOD(MineLayer, wr_think, void(entity thiswep, entity actor, .entity weaponentity, int fire))
		{
			if(autocvar_g_balance_minelayer_reload_ammo && actor.clip_load < WEP_CVAR(minelayer, ammo)) // forced reload
			{
				// not if we're holding the minelayer without enough ammo, but can detonate existing mines
				if(!(W_MineLayer_PlacedMines(false) && actor.(thiswep.ammo_field) < WEP_CVAR(minelayer, ammo))) {
					thiswep.wr_reload(thiswep, actor, weaponentity);
				}
			}
			else if(fire & 1)
			{
				if(weapon_prepareattack(thiswep, actor, weaponentity, false, WEP_CVAR(minelayer, refire)))
				{
					W_MineLayer_Attack(thiswep);
					weapon_thinkf(actor, weaponentity, WFRAME_FIRE1, WEP_CVAR(minelayer, animtime), w_ready);
				}
			}

			if(fire & 2)
			{
				if(W_MineLayer_PlacedMines(true))
					sound(actor, CH_WEAPON_B, SND_MINE_DET, VOL_BASE, ATTN_NORM);
			}
		}
		METHOD(MineLayer, wr_checkammo1, bool(entity thiswep))
		{
			int slot = 0; // TODO: unhardcode
			// don't switch while placing a mine
			if(ATTACK_FINISHED(self, slot) <= time || PS(self).m_weapon != WEP_MINE_LAYER)
			{
				float ammo_amount = self.(thiswep.ammo_field) >= WEP_CVAR(minelayer, ammo);
				ammo_amount += self.(weapon_load[WEP_MINE_LAYER.m_id]) >= WEP_CVAR(minelayer, ammo);
				return ammo_amount;
			}
			return true;
		}
		METHOD(MineLayer, wr_checkammo2, bool(entity thiswep))
		{
			if(W_MineLayer_PlacedMines(false))
				return true;
			else
				return false;
		}
		METHOD(MineLayer, wr_resetplayers, void(entity thiswep))
		{
			self.minelayer_mines = 0;
		}
		METHOD(MineLayer, wr_reload, void(entity thiswep, entity actor, .entity weaponentity))
		{
			W_Reload(self, WEP_CVAR(minelayer, ammo), SND(RELOAD));
		}
		METHOD(MineLayer, wr_suicidemessage, int(entity thiswep))
		{
			return WEAPON_MINELAYER_SUICIDE;
		}
		METHOD(MineLayer, wr_killmessage, int(entity thiswep))
		{
			return WEAPON_MINELAYER_MURDER;
		}

#endif
#ifdef CSQC

		METHOD(MineLayer, wr_impacteffect, void(entity thiswep))
		{
			vector org2;
			org2 = w_org + w_backoff * 12;
			pointparticles(EFFECT_ROCKET_EXPLODE, org2, '0 0 0', 1);
			if(!w_issilent)
				sound(self, CH_SHOTS, SND_MINE_EXP, VOL_BASE, ATTN_NORM);
		}

#endif
#endif
