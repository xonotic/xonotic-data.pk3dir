#pragma once

#include "progname.qh"

#define assert(expr, ...) _assert(LOG_SEVERE, expr, __VA_ARGS__)
#define devassert(...) MACRO_BEGIN if (autocvar_developer) assert(__VA_ARGS__); MACRO_END

#define assert_once(expr, ...) \
	MACRO_BEGIN \
		static bool __once; \
		if (!__once) \
		{ \
			assert(expr, __VA_ARGS__); \
			__once = true; \
		} \
	MACRO_END
#define devassert_once(...) MACRO_BEGIN if (autocvar_developer) assert_once(__VA_ARGS__); MACRO_END

#define demand(expr, ...) _assert(LOG_FATAL, expr, __VA_ARGS__)
#define devdemand(...) MACRO_BEGIN if (autocvar_developer) demand(__VA_ARGS__); MACRO_END

#define _assert(f, expr, then) \
	MACRO_BEGIN \
		if (!(expr)) \
		{ \
			f("assertion failed: `" #expr "`\n"); \
			then; \
		} \
	MACRO_END

#define ASSERT_LESS(name, var, const) noref int name[(const - var + 1)];


#if defined(MENUQC)
string(string, string...) strcat1n = #53;
#else
string(string, string...) strcat1n = #115;
#endif

// would be nice if __FUNC__ could be concatenated at compile time
#if 0
	// less work, bigger binary
	#define __SOURCELOC__ (sprintf("^7%s^9" "(" "^9"__FILE__"^7"  ":"  "^9"STR(__LINE__)"^7" ")", __FUNC__))
#else
	#define __SOURCELOC__ (sprintf("^7%s^9" "(" "^9%s^7"  ":"  "^9%s^7" ")", __FUNC__, __FILE__, STR(__LINE__)))
#endif

#define _LOG_HEADER(level) "^9[::" "^7"PROGNAME"^9" "::" level"^9" "] ", __SOURCELOC__
#define _LOG(f, level, s) \
	MACRO_BEGIN \
		f(strcat1n(_LOG_HEADER(level), "\n^7", s, "\n")); \
	MACRO_END

#define  LOG_FATAL(...) _LOG_FATAL(strcat1n(__VA_ARGS__))
#define  LOG_FATALF(...) _LOG_FATAL(sprintf(__VA_ARGS__))
#define _LOG_FATAL(s) _LOG(error, "^1FATAL", s)

#define  LOG_SEVERE(...) _LOG_SEVERE(strcat1n(__VA_ARGS__))
#define  LOG_SEVEREF(...) _LOG_SEVERE(sprintf(__VA_ARGS__))
#define _LOG_SEVERE(s) _LOG(backtrace, "^1SEVERE", s)

#define  LOG_WARN(...) _LOG_WARN(strcat1n(__VA_ARGS__))
#define  LOG_WARNF(...) _LOG_WARN(sprintf(__VA_ARGS__))
#define _LOG_WARN(s) _LOG(print, "^3WARNING", s)

#define  LOG_INFO(...) _LOG_INFO(strcat1n(__VA_ARGS__))
#define  LOG_INFOF(...) _LOG_INFO(sprintf(__VA_ARGS__))
#define _LOG_INFO(s) \
	MACRO_BEGIN \
		dprint(_LOG_HEADER("^5INFO")); \
		string __s = s; \
		print("\n^7", __s); \
		/* TODO: unconditionally add a newline when possible */ \
		if (str2chr(__s, strlen(__s) - 1) != '\n') { print("\n"); } \
	MACRO_END

#define  LOG_TRACE(...) _LOG_TRACE(strcat1n(__VA_ARGS__))
#define  LOG_TRACEF(...) _LOG_TRACE(sprintf(__VA_ARGS__))
#define _LOG_TRACE(s) _LOG(dprint, "^6TRACE", s)

#define  LOG_DEBUG(...) _LOG_DEBUG(strcat1n(__VA_ARGS__))
#define  LOG_DEBUGF(...) _LOG_DEBUG(sprintf(__VA_ARGS__))
#define _LOG_DEBUG(s) _LOG(dprint2, "^2DEBUG", s)

#define dprint2(msg) \
	MACRO_BEGIN \
		if (autocvar_developer > 1) dprint(msg); \
	MACRO_END

// TODO: this sucks, lets find a better way to do backtraces?
#define _backtrace() builtin_remove(NULL)

noref int autocvar_developer;
noref bool autocvar_prvm_backtraceforwarnings;

#ifdef SVQC
#define bt_cvar_set(cvar, value) builtin_cvar_set(cvar, value)
#else
#define bt_cvar_set(cvar, value) cvar_set(cvar, value)
#endif

#define backtrace(msg) \
	MACRO_BEGIN \
		int dev = autocvar_developer; \
		bool war = autocvar_prvm_backtraceforwarnings; \
		bt_cvar_set("developer", "1"); \
		bt_cvar_set("prvm_backtraceforwarnings", "1"); \
		print("\n--- CUT HERE ---\n", msg); \
		_backtrace(); \
		print("\n--- CUT UNTIL HERE ---\n"); \
		bt_cvar_set("developer", ftos(dev)); \
		bt_cvar_set("prvm_backtraceforwarnings", ftos(war)); \
	MACRO_END
